import "card.sq"

myth Pile {
	equip cards

	recite create() {
		local deck = []
		local i = 0

		whilst i < 52 {
			deck[i] = Card(i % 13, i / 13)
			i = i + 1
		}

		reward my(deck)
	}

	journey shuffle() {
		local len = my.length()
		local n = 100 + (random() % 300)
		local cards = my.cards

		whilst n {
			i = random() % len
			j = random() % len

			tmp = cards[i]
			cards[i] = cards[j]
			cards[j] = tmp

			n = n - 1
		}
	}

	journey length() {
		reward length(my.cards)
	}

	journey to_string() {
		renowned cardstr
		local i = 0
		local len = my.length()
		local str = ""

		whilst i < len {
			local card = my.cards[i];

			str = str + card.to_string() + " "
			i = i + 1
		}

		reward substr(str, 0, length(str) - 1)
	}

	journey draw() {
		reward delete(my.cards, 0)
	}

	journey add(card) {
		insert(my.cards, my.length(), card)
	}
}
