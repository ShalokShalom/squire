import "card.sq"

myth Pile {
	equip cards

	recite create() {
		nigh deck = []
		nigh i = N

		whilst i < LII {
			deck[i] = Card(i % XIII, i / XIII)
			i = i + I
		}

		reward Pile(deck)
	}

	journey shuffle() {
		nigh len = thine.length()
		nigh n = C + (random() % CCC)
		nigh cards = thine.cards

		whilst n {
			i = random() % len
			j = random() % len

			tmp = cards[i]
			cards[i] = cards[j]
			cards[j] = tmp

			n = n - I
		}
	}

	journey length() {
		reward length(thine.cards)
	}

	journey to_string() {
		renowned cardstr
		nigh i = N
		nigh len = thine.length()
		nigh str = ""

		whilst i < len {
			nigh card = thine.cards[i];

			str = str + card.to_string() + " "
			i = i + I
		}

		reward substr(str, N, length(str) - I)
	}

	journey draw() {
		reward delete(thine.cards, N)
	}

	journey add(card) {
		insert(thine.cards, thine.length(), card)
	}
}
