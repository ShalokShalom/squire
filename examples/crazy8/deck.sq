import "card.sq"

form Pile {
	matter cards

	describe create() {
		nigh deck = []
		nigh i = N

		whilst i < LII {
			deck[i] = Card(i % XIII, i / XIII)
			i = i + I
		}

		reward Pile(deck)
	}

	action shuffle() {
		nigh len = thine.length()
		nigh n = C + (random() % CCC)
		nigh cards = thine.cards

		whilst n {
			i = random() % len
			j = random() % len

			tmp = cards[i]
			cards[i] = cards[j]
			cards[j] = tmp

			n = n - I
		}
	}

	action length() {
		reward length(thine.cards)
	}

	action to_string() {
		renowned cardstr
		nigh i = N
		nigh len = thine.length()
		nigh str = ""

		whilst i < len {
			nigh card = thine.cards[i];

			str = str + card.to_string() + " "
			i = i + I
		}

		reward substr(str, N, length(str) - I)
	}

	action draw() {
		reward delete(thine.cards, N)
	}

	action add(card) {
		insert(thine.cards, thine.length(), card)
	}
}
